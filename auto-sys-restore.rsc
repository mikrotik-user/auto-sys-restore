# Time in minutes before script restores router configuration
:global restoreTime 10m

:if ( [ :typeof "$restoreTime" ] != "time" ) do={
    :log warn ( "auto-sys-restore: required time value is not specified or has incorrect value. Using default - 10 minutes" )
    :set restoreTime 10m
}

:local cdate [ /system clock get date ] 
:local ctime [ /system clock get time ] 
:local bckpFile "auto-sys-restore-auto"

:if ( [ :len [ file find where type="disk" and name="flash" ] ] != 0 ) do={ :set bckpFile "flash/auto-sys-restore" }
:do { /system backup save dont-encrypt=yes name="$bckpFile" } on-error={ :error "auto-sys-restore: can't create binary backup" }

/system scheduler 
     :do { remove [ find name="auto-sys-restore-in-time" ] } on-error={} 
     :do { remove [ find name="auto-sys-restore-after-reboot" ] } on-error={}
     :do { add comment="Auto restore in time period. Generated by auto-sys-restore script" \
	           name="auto-sys-restore-in-time" \
		       start-date=$cdate start-time=[ ( $ctime + $restoreTime ) ] \
		       on-event="/system scheduler remove [ find name=\"auto-sys-restore-after-reboot\" ] \r\n\
		                 /system backup load name=\"$bckpFile.backup\" password=\"\"" disabled=no } on-error={:error "Can't create auto-sys-restore-in-time scheduler"}
     :do { add comment="Auto restore after reboot. Generated by auto-sys-restore script"\
	           name="auto-sys-restore-after-reboot" \
		       start-time=startup \
		       on-event="/system backup load name=\"$bckpFile.backup\" password=\"\"" disabled=no } on-error={:error "Can't create auto-sys-restore-after-reboot scheduler"}

:log warn ( "auto-sys-restore: system backup restore is scheduled in $restoreTime (file: $bckpFile.backup)" )
:log warn ( "auto-sys-restore: execute script no-autosys-restore to remove scheduled restore event" )
:put ( "auto-sys-restore: System backup restore is scheduled in $restoreTime (file: $bckpFile.backup)" )
:put ( "auto-sys-restore: execute script no-autosys-restore to remove scheduled restore event" )
/system script
     add comment="This script will remove scheduled event for rollback system" dont-require-permissions=no name=no-autosys-restore owner=admin policy=ftp,reboot,read,write,policy,test,password,sniff,sensitive,romon \
	     source=":do { /system scheduler remove [ find name=\"auto-sys-restore-in-time\" ] } on-error={:error \"Can't remove scheduler auto-sys-restore-in-time\"} \r\
               \n:do { /system scheduler remove [ find name=\"auto-sys-restore-after-reboot\" ] } on-error={:error \"Can't remove scheduler auto-sys-restore-after-reboot\"}\r\
               \n:log warn \"restore-in-time event has been removed\"\r\
			   \n:put \"restore-in-time event has been removed\"\r\
			   \n:do { /system script remove [ find name=\"no-autosys-restore\" ]} on-error={}\r\
			   \n:do { /file remove [ find name=\"auto-sys-restore-auto.backup\" ]} on-error={}"

/system script environment remove [ find name="restoreTime" ]
